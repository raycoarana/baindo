apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.1.201405082137"
}

// get class dirs for project dependencies
FileTree getJacocoClassDirs(List projects) {
    def classDirs = fileTree(dir: "${buildDir}/intermediates/classes/debug",
                             excludes: ['**/BuildConfig.class', '**/R.class', '**/R$*.class'])
    projects.each {
        def projBuildDir = project(it).buildDir
        classDirs += fileTree(dir: "${projBuildDir}/intermediates/classes/release",
                              excludes: ['**/BuildConfig.class', '**/R.class', '**/R$*.class'])
    }
    return classDirs
}

// get source dirs for project dependencies
FileCollection getJacocoSrcDirs(List projects) {
    Set srcDirs = android.sourceSets.main.java.srcDirs
    projects.each {
        def projDir = project(it).projectDir
        srcDirs.add("${projDir}/src") // assume that android main sourceSets is here
    }
    return files(srcDirs)
}

// generate coverage report for this project and all its project dependencies
task jacocoTestReport(type: JacocoReport, dependsOn: test) {
    reports {
        xml.enabled = true
        csv.enabled = false
        xml.destination = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        html.destination = "${buildDir}/reports/coverage"
    }

    // TODO: automatically get project dependencies recursively
    def dependencies = [] // your gradle project dependencies go here
    classDirectories = getJacocoClassDirs(dependencies)
    sourceDirectories = getJacocoSrcDirs(dependencies)
    executionData = files("${buildDir}/jacoco/testDebug.exec")
}